buildscript {

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

ext {
    lombokVersion = '1.18.0'

    postgreslDriverVersion = '9.1-901.jdbc4'

    gdxVersion = '1.7.0'
    guavaVersion = '23.5-jre'
    JUGVersion = '3.1.4'

    junitVersion = '4.12'
    testngVersion = '6.8'
    hamcrestVersion = '1.3'
    mockitoVersion = '2.10.0'
    hamcrestDateVersion = '2.0.4'
    javaHamcrestVersion = '2.0.0.0'
    commonsIOVersion = '2.5'

    testngVersion = '6.8'
    dbUnitVersion = '2.5.3'
    springTestDbUnitVersion = '1.2.0'
    hsqldbVersion = '2.3.4'
}

allprojects {
    apply plugin: 'idea'

    group 'com.droidwars'
    version = '0.2'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava]*.options.collect { options ->
        options.encoding = 'UTF-8'
        options.compilerArgs = ['-Xlint:none']
    }

    def loadProperties = { String fileName ->
        def props = new Properties()
        def propFile = file(fileName)
        if (propFile.canRead()) {
            propFile.withInputStream { props.load(it) }
            props.each { project.setProperty(it.key, it.value) }
        }
    }

    loadProperties 'gradle-local.properties'

}

task webRunWithoutAngularRebuild{
    group 'droid-wars'
    description 'Run web-site, do not rebuild angular application'
    dependsOn ':site:web:bootRun'
}

task webRun{
    group 'droid-wars'
    description 'Run web-site'
    // Build frontend app before running SpringBoot
    dependsOn(':site:frontend:buildClientDev')

    dependsOn(':site:web:bootRun')
}

task serverRun{
    group 'droid-wars'
    description 'Run game execution server'

    dependsOn ':server:bootRun'
}

task testAll(type: GradleBuild) {
    group 'droid-wars'
    description 'Execute all tests'

    tasks = ['clean', 'test']
}
